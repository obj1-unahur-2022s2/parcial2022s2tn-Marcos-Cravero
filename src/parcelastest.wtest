import parcelas.*
import plantas.*

describe "Test de Parcelas"  {
	
	const menta1 = new Menta(anioDeObtencion = 2022, altura = 1)
	const menta2 = new Menta(anioDeObtencion = 2022, altura = 0.3)
	const soja1 = new Soja(anioDeObtencion = 2009, altura = 0.6)
	const soja2 = new Soja(anioDeObtencion = 2009, altura = 1.6)
	const quinoa1 = new Quinoa(tolerancia = 12, anioDeObtencion = 2010, altura = 1)
	const quinoa3 = new Quinoa(tolerancia = 5, anioDeObtencion = 2001, altura = 1)
	
	const sojaTransgenica1 = new SojaTransgenica(anioDeObtencion = 2009, altura = 0.6)
	const hierbabuena1 = new Hierbabuena(anioDeObtencion = 2022, altura = 1)
	
	const parcela1 = new Parcela(ancho = 20, largo = 1, horasDeSol = 8)
	const parcela2 = new Parcela(ancho = 20, largo = 1, horasDeSol = 7)
	const parcela3 = new Parcela(ancho = 5, largo = 1, horasDeSol = 6)
	
	const parcelaEcologica1 = new ParcelaEcologica(ancho = 20, largo = 1, horasDeSol = 7)
	const parcelaIndustrial1 = new ParcelaIndustrial(ancho = 10, largo = 1, horasDeSol = 7)
	
	
	test "Parcela de 20x1m que recibe 8 horas de sol, tiene 20m2 de superficie y tolera hasta 4" {
		
		assert.equals(20, parcela1.superficie())
		assert.equals(4, parcela1.cantidadMaxima())
	}
	
	test "Parcela1 no tiene complicaciones con 4, pero sí con 5 plantas (por espacio)" {
		
		parcela1.plantar(soja2)
		parcela1.plantar(soja2)
		parcela1.plantar(soja2)
		parcela1.plantar(soja2)
		
		assert.notThat(parcela1.tieneComplicaciones())
		
		parcela1.plantar(soja2)
		
		assert.throwsExceptionWithMessage("No hay más espacio para plantar", {parcela1.plantar(soja2)})
	}
	
	test "Quinoa3 no podría con el calor del sol de parcela1" {

		assert.throwsExceptionWithMessage("La planta no tolera las horas de calor", {parcela1.plantar(quinoa3)})
	}
	
	test "Parcela ideal de la Menta y Hierbabuena" {
		
		assert.that(menta1.parcelaIdeal(parcela1))
		assert.that(hierbabuena1.parcelaIdeal(parcela1))
	}
	
	test "Parcela ideal de la Quinoa" {
		
		parcela1.plantar(soja1)
		parcela1.plantar(menta1)
		parcela1.plantar(menta2)
		
		assert.that(quinoa1.parcelaIdeal(parcela1))
		
		parcela1.plantar(soja2)
		
		assert.notThat(quinoa1.parcelaIdeal(parcela1))
	}
	
	test "Parcela ideal de la Soja" {
		
		assert.that(soja1.parcelaIdeal(parcela2))	
		assert.notThat(soja1.parcelaIdeal(parcela1))
	}
	
	test "Parcela ideal de la Soja transegénica" {
		
			
		assert.that(sojaTransgenica1.parcelaIdeal(parcela3))
	}
	
	test "La Soja se asocia bien con una Parcela ecológica"  {
		
		parcelaEcologica1.plantar(soja1)
		
		assert.that(parcelaEcologica1.seAsociaBien(soja1))
	}
	
	test "La Quinoa se asocia bien con una Parcela industrial" {
		
		parcelaIndustrial1.plantar(quinoa1)
		
		assert.that(parcelaIndustrial1.seAsociaBien(quinoa1))
	}
}
	